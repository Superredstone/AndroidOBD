apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlinx-serialization'
apply plugin: 'org.jetbrains.dokka'
apply plugin: "com.vanniktech.maven.publish"

version = "1.0.4"
group = "com.pnuema.android"

ext {
    baseUrl = 'https://github.com/barnhill'
    siteUrl = 'https://github.com/barnhill/AndroidOBD'
    gitUrl = 'https://github.com/barnhill/AndroidOBD.git'
    descr = 'Android library to communicate with ELM327 based OBD devices'
}

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 30
        versionCode 1
        versionName version
    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
        useIR = true
    }

    signingConfigs {
        release {
            storeFile file(System.properties['virtualDynoCertLocation'])
            storePassword System.properties['virtualDynoKeystorePassword']
            keyAlias "virtualdyno_release"
            keyPassword System.properties['virtualDynoKeystorePassword']
        }
    }
}

dependencies {
    implementation 'com.udojava:EvalEx:2.6'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.0.1'
    implementation "androidx.startup:startup-runtime:1.1.0"
}

dokkaHtml.configure {
    dokkaSourceSets {
        named("main") {
            noAndroidSdkLink.set(false)
        }
    }
}

task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from android.sourceSets.main.java.srcDirs
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier.set("javadoc")
    from dokkaJavadoc.outputDirectory
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

build.dependsOn dokkaJavadoc
